plugins {
    id "de.undercouch.download" version "3.2.0"
}

apply plugin: 'base'
version = '0.2'

ext {
    gradleVersion = '3.4.1'
    companyName = 'company'
    gradleDownloadBase = 'http://services.gradle.org/distributions/'
    gradleClassifier = 'bin'
}

import de.undercouch.gradle.tasks.download.Download

task downloadGradle(type: Download) {
    description = 'Downloads the Gradle distribution with a given version.'

    src gradleDownloadBase + "gradle-"+gradleVersion+"-"+ gradleClassifier + ".zip"
    dest "$buildDir/gradle-download/gradle-"+gradleVersion+"-" + gradleClassifier + ".zip"
    onlyIfNewer true
}

task customGradleDistribution(type: Zip, dependsOn: downloadGradle) {
    description = 'Builds custom Gradle distribution and bundles initialization scripts.'
    archiveName "$companyName-gradle-$project.gradleVersion-$version-${project.gradleClassifier}.zip"

    from zipTree(downloadGradle.dest)
    into "gradle-$project.gradleVersion/init.d", {
        from 'src/init.d'
    }
}

defaultTasks 'customGradleDistribution'